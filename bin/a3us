#!/usr/bin/perl -w

=head1 NAME

a3us - A3 Usage Sample

=head1 SYNOPSIS

Records current used capacity

=cut

use warnings;
use strict;

use lib '/usr/local/pf/lib';

use POSIX qw(strftime);

use pf::error qw(is_success);
use pf::log;
use pf::cluster;
use pf::node;
use pf::dal::a3_usage_sample;
use pf::config qw(%Config);

BEGIN {
    #Ensure that the file permissions of the log file is correct 0660
    umask (0);
    use pf::log (service => 'a3us');
}

our $PROGRAM_NAME = $0 = 'a3us';

my $logger = get_logger( $PROGRAM_NAME );

if ($pf::cluster::cluster_enabled && ! pf::cluster::is_management()) {
    $logger->info("Not taking samples on non-management node.");
    exit 0;
}

my $now   = strftime( "%Y-%m-%d %H:%M:%S", gmtime(time()) );
my $count = pf::node::node_count_active();

$logger->info("Used capacity at $now: $count");

my $status = pf::dal::a3_usage_sample->create({
    timestamp => $now,
    count     => $count
});

if (! is_success($status)) {
    $logger->error("Failed to record usage sample at $now");
}

#clean up the db for us that is outdated based on the db_us_clean_threshold config
my $one_month_ago = 60 * 60 * 24 * $Config{A3}->{db_us_clean_threshold};
my $dal_now = pf::dal->now();
my ($remove_status, $rows) = pf::dal::a3_usage_sample->remove_items(
    -where => {
        timestamp => {
            "<" => \[ 'DATE_SUB(?, INTERVAL ? SECOND)', $dal_now, $one_month_ago ]
        }
    }
);
if(is_success($remove_status)) {
    $logger->info("Removed $rows entry from usage sample db");
} else {
    $logger->error("Failed to clean up the usage sample db");
}